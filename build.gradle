import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask

plugins {
    id("com.github.ben-manes.versions") version "0.43.0"
}

description = 'kafka-connect-rest-source'

allprojects {
    group = "org.radarbase"
    version = "0.4.1-SNAPSHOT"

    repositories {
        mavenCentral()
        maven { url "https://packages.confluent.io/maven/" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }
}

subprojects {
    ext {
        kafkaVersion = '7.2.2-ce'
        confluentVersion = '7.2.2'
        jacksonVersion = '2.13.2'
    }

    apply plugin: 'java'
    apply plugin: 'java-library'

    sourceCompatibility = 11
    targetCompatibility = 11
}

wrapper {
    gradleVersion '7.5.1'
}

evaluationDependsOnChildren()

task downloadDependencies {
    subprojects.collect {
        it.configurations.runtimeClasspath.files
        it.configurations.compileClasspath.files
    }

    doLast {
        println 'Downloaded REST code dependencies'
    }
}


def isNonStable = { String version ->
    def stableKeyword = ["RELEASE", "FINAL", "GA", "-CE"].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
